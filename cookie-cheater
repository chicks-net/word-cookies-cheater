#!/usr/bin/perl

use strict;
use warnings;
use Data::Dumper;
use IO::Prompt;

#print "hello world\n";
#print Dumper( letter_count('hello') );
my %linux_words; # letter counts from Linux
read_dict_words();
my $linux_count = scalar keys %linux_words;
print "got $linux_count words from Linux\n";

my $letter_count = 0;
while( $letter_count == 0 ) {
	my $got = prompt "number of letters in word: ";
	if ($got =~ /^\d+$/ or $got =~ /^\d+-\d+/) {
		$letter_count = $got;
	} else {
		warn "'$got' is not a number"
	}
}

my $letters_available = prompt "cookie letters: ";
my $available_count = letter_count($letters_available);
seek_words(\%linux_words,$available_count,$letter_count);

sub seek_words {
	my ($dict,$avail,$size) = @_;

	my($min,$max);
	if ($size =~ /(\d+)-(\d+)/) {
		$min = $1;
		$max = $2;
	} else {
		$min = $size;
		$max = $size;
	}

	my %found_words;
	WORD: foreach my $word (keys %$dict) {
		# only check words that are the right length
		my $len = length($word);
		next unless $len >= $min;
		next unless $len <= $max;

		my $checking = $dict->{$word};

		# do we have enough letters to make the word?
		foreach my $letter (keys %$checking) {
			if (!defined($avail->{$letter})
				or $avail->{$letter} < $checking->{$letter}
			) {
				# it doesn't work
				next WORD;
			}
		}

		push(@{$found_words{$len}}, $word);
	}

	if (scalar keys %found_words) {
		foreach my $len (sort {$a<=>$b} keys %found_words) {
			my $c = scalar @{$found_words{$len}};
			print "\n";
			print "$c words of $len characters matched:\n";
			foreach my $word (sort @{$found_words{$len}}) {
				print "- $word\n";
			}
		}
	} else {
		print "NO words found! :(\n";
	}
}


sub read_dict_words {
	my $dict = '/usr/share/dict/words';

	open(my $words, '<', $dict) or die "open($dict): $!";

	while (my $word = <$words>) {
		chomp($word);
		$linux_words{$word} = letter_count($word);
	}

	close($words) or die "close($dict): $!";
}

sub letter_count {
	my ($string) = @_;

	my %counts;
	foreach my $char ( split //, $string ) {
		$counts{$char}++;
	}

	return \%counts;
}
